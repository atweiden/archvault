#!/usr/bin/env perl6




use v6;
use Archvault;




# -----------------------------------------------------------------------------
# help
# -----------------------------------------------------------------------------

multi sub MAIN('help', 'ls' --> Nil)
{
    USAGE('ls');
}

multi sub MAIN('help', 'new' --> Nil)
{
    USAGE('new');
}

multi sub MAIN('help', $? --> Nil)
{
    USAGE();
}




# -----------------------------------------------------------------------------
# new
# -----------------------------------------------------------------------------

multi sub MAIN(
    'new',
    *%opts (
        Bool :augment($),
        Str :disktype($),
        Str :graphics($),
        Str :hostname($),
        Str :keymap($),
        Str :locale($),
        Str :partition($),
        Str :processor($),
        Bool :reflector($),
        Str :rootpass($),
        Str :timezone($),
        Str :username($),
        Str :userpass($),
        Str :vaultname($),
        Str :vaultpass($)
    )
    --> Nil
)
{
    Archvault.new(|%opts);
}




# -----------------------------------------------------------------------------
# ls
# -----------------------------------------------------------------------------

multi sub MAIN('ls', 'keymaps' --> Nil)
{
    Archvault::Config.ls-keymaps().map({ .say() });
}

multi sub MAIN('ls', 'locales' --> Nil)
{
    Archvault::Config.ls-locales().map({ .say() });
}

multi sub MAIN('ls', 'partitions' --> Nil)
{
    Archvault::Config.ls-partitions().map({ .say() });
}

multi sub MAIN('ls', 'timezones' --> Nil)
{
    Archvault::Config.ls-timezones().map({ .say() });
}




# -----------------------------------------------------------------------------
# version
# -----------------------------------------------------------------------------

multi sub MAIN(Bool:D :version($)! where *.so() --> Nil)
{
    printf("v%s\n", $Archvault::VERSION);
    exit(0);
}




# -----------------------------------------------------------------------------
# usage
# -----------------------------------------------------------------------------

multi sub USAGE(--> Nil)
{
    constant $HELP = q:to/EOF/.trim();
    Usage:
      archvault new
      archvault ls <keymaps|locales|partitions|timezones>

    Commands:
      help                        Show help for subcommands
      ls                          List keymaps, locales, partitions, timezones
      new                         Bootstrap Arch system with FDE

    Options:
      -h, --help                  Print this help message
      --version                   Print version and exit
    EOF
    say($HELP);
}

multi sub USAGE('new' --> Nil)
{
    constant $HELP = q:to/EOF/.trim();
    Usage:
      archvault --username="live"                     \
                --userpass="your new user's password" \
                --rootpass="your root password"       \
                --vaultname="luckbox"                 \
                --vaultpass="your vault password"     \
                --hostname="luckbox"                  \
                --partition="/dev/sdb"                \
                --processor="other"                   \
                --graphics="intel"                    \
                --disktype="usb"                      \
                --locale="en_US"                      \
                --keymap="us"                         \
                --timezone="America/Los_Angeles"      \
                --augment                             \
                new

    Options:
      --augment                    drop to Bash console mid-execution
      --disktype=<disktype>        hard drive type
      --graphics=<graphics>        graphics card type
      --hostname=<hostname>        hostname
      --keymap=<keymap>            keymap
      --locale=<locale>            locale
      --partition=<device-path>    partition target for install
      --processor=<processor>      processor type
      --reflector                  optimize pacman mirrors
      --rootpass=<password>        root password
      --timezone=<timezone>        timezone
      --username=<username>        user name
      --userpass=<password>        user password
      --vaultname=<vault-name>     vault name
      --vaultpass=<password>       vault password
    EOF
    say($HELP);
}

multi sub USAGE('ls' --> Nil)
{
    constant $HELP = q:to/EOF/.trim();
    Usage:
      archvault ls keymaps
      archvault ls locales
      archvault ls partitions
      archvault ls timezones
    EOF
    say($HELP);
}

# vim: set filetype=perl6 foldmethod=marker foldlevel=0:
