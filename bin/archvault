#!/usr/bin/env perl6




use v6;
use Archvault;




# -----------------------------------------------------------------------------
# help
# -----------------------------------------------------------------------------

multi sub MAIN('help', 'install')
{
    USAGE('install');
}

multi sub MAIN('help', 'ls')
{
    USAGE('ls');
}

multi sub MAIN('help', $?)
{
    USAGE();
}




# -----------------------------------------------------------------------------
# install
# -----------------------------------------------------------------------------

multi sub MAIN(
    'install',
    *%opts (
        Bool :augment($),
        Str :disktype($),
        Str :graphics($),
        Str :hostname($),
        Str :keymap($),
        Str :locale($),
        Str :partition($),
        Str :processor($),
        Str :rootpass($),
        Str :timezone($),
        Str :username($),
        Str :userpass($),
        Str :vaultname($),
        Str :vaultpass($)
    )
)
{
    Archvault.install(|%opts);
}




# -----------------------------------------------------------------------------
# ls
# -----------------------------------------------------------------------------

multi sub MAIN('ls', 'keymaps') { .say for Archvault::Config.ls-keymaps }
multi sub MAIN('ls', 'locales') { .say for Archvault::Config.ls-locales }
multi sub MAIN('ls', 'partitions') { .say for Archvault::Config.ls-partitions }
multi sub MAIN('ls', 'timezones') { .say for Archvault::Config.ls-timezones }




# -----------------------------------------------------------------------------
# version
# -----------------------------------------------------------------------------

multi sub MAIN(Bool:D :version($)! where *.so)
{
    say('v' ~ $Archvault::VERSION);
    exit;
}




# -----------------------------------------------------------------------------
# usage
# -----------------------------------------------------------------------------

multi sub USAGE()
{
    constant $HELP = q:to/EOF/.trim;
    Usage:
      archvault [-h] install

    Commands:
      help                        Show help for subcommands
      install                     Bootstrap Arch system with FDE
      ls                          List discovered keymaps, locales, partitions,
                                  timezones

    Options:
      -h, --help                  print this help message
      --version                   print version and exit
    EOF
    say($HELP);
}

multi sub USAGE('install')
{
    constant $HELP = q:to/EOF/.trim;
    Usage:
      archvault --username="live"                     \
                --userpass="your new user's password" \
                --rootpass="your root password"       \
                --vaultname="luckbox"                 \
                --vaultpass="your vault password"     \
                --hostname="luckbox"                  \
                --partition="/dev/sdb"                \
                --processor="other"                   \
                --graphics="intel"                    \
                --disktype="usb"                      \
                --locale="en_US"                      \
                --keymap="us"                         \
                --timezone="America/Los_Angeles"      \
                --augment                             \
                install

    Options:
      --augment                    drop to Bash console mid-execution
      --disktype=<disktype>        hard drive type
      --graphics=<graphics>        graphics card type
      --hostname=<hostname>        hostname
      --keymap=<keymap>            keymap
      --locale=<locale>            locale
      --partition=<device-path>    partition target for install
      --processor=<processor>      processor type
      --rootpass=<password>        root password
      --timezone=<timezone>        timezone
      --username=<username>        user name
      --userpass=<password>        user password
      --vaultname=<vault-name>     vault name
      --vaultpass=<password>       vault password
    EOF
    say($HELP);
}

multi sub USAGE('ls')
{
    constant $HELP = q:to/EOF/.trim;
    Usage:
      archvault ls keymaps
      archvault ls locales
      archvault ls partitions
      archvault ls timezones
    EOF
    say($HELP);
}

# vim: set filetype=perl6 foldmethod=marker foldlevel=0:
