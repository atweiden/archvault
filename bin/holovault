#!/usr/bin/env perl6




use v6;
use Holovault;




# -----------------------------------------------------------------------------
# help
# -----------------------------------------------------------------------------

multi sub MAIN('help', Str $command)
{
    given $command
    {
        when 'install'
        {
            USAGE('install');
        }
        when 'ls'
        {
            USAGE('ls');
        }
        default
        {
            USAGE();
        }
    }
}




# -----------------------------------------------------------------------------
# install
# -----------------------------------------------------------------------------

multi sub MAIN(
    'install',
    *%opts (
        Bool :$augment,
        Str :$disktype,
        Str :$graphics,
        Str :$holograms,
        Str :$holograms-dir,
        Str :$hostname,
        Str :$keymap,
        Str :$locale,
        Str :$partition,
        Str :$processor,
        Str :$rootpass,
        Str :$timezone,
        Str :$username,
        Str :$userpass,
        Str :$vaultname,
        Str :$vaultpass,
    )
)
{
    Holovault.install(|%opts);
}




# -----------------------------------------------------------------------------
# ls
# -----------------------------------------------------------------------------

multi sub MAIN('ls', 'holograms', *%opts (Str :$holograms-dir))
{
    .say for Holovault::Config.ls-holograms(|%opts);
}
multi sub MAIN('ls', 'keymaps') { .say for Holovault::Config.ls-keymaps; }
multi sub MAIN('ls', 'locales') { .say for Holovault::Config.ls-locales; }
multi sub MAIN('ls', 'partitions') { .say for Holovault::Config.ls-partitions; }
multi sub MAIN('ls', 'timezones') { .say for Holovault::Config.ls-timezones; }




# -----------------------------------------------------------------------------
# version
# -----------------------------------------------------------------------------

multi sub MAIN(Bool:D :$version! where *.so)
{
    say 'v' ~ $Holovault::VERSION;
    exit;
}




# -----------------------------------------------------------------------------
# usage
# -----------------------------------------------------------------------------

multi sub USAGE()
{
    constant $HELP = q:to/EOF/;
    Usage:
      holovault [-h] install

    Commands:
      help                        Show help for subcommands
      install                     Bootstrap Arch system with Holo
      ls                          List discovered holograms, keymaps, locales,
                                  partitions, timezones

    Options:
      -h, --help                  print this help message
      --version                   print version and exit
    EOF
    say $HELP.trim;
}

multi sub USAGE('install')
{
    constant $HELP = q:to/EOF/;
    Usage:
      holovault --holograms="skreltoi amnesia"        \
                --username="live"                     \
                --userpass="your new user's password" \
                --rootpass="your root password"       \
                --vaultname="luckbox"                 \
                --vaultpass="your vault password"     \
                --hostname="luckbox"                  \
                --partition="/dev/sdb"                \
                --processor="other"                   \
                --graphics="intel"                    \
                --disktype="usb"                      \
                --locale="en_US"                      \
                --keymap="us"                         \
                --timezone="America/Los_Angeles"      \
                --augment                             \
                install

    Options:
      --augment                    drop to Bash console mid-execution
      --disktype=<disktype>        hard drive type
      --graphics=<graphics>        graphics card type
      --holograms=<holograms>      holograms
      --holograms-dir=<dir>        directory containing hologram subdirs
      --hostname=<hostname>        hostname
      --keymap=<keymap>            keymap
      --locale=<locale>            locale
      --partition=<device-path>    partition target for install
      --processor=<processor>      processor type
      --rootpass=<password>        root password
      --timezone=<timezone>        timezone
      --username=<username>        user name
      --userpass=<password>        user password
      --vaultname=<vault-name>     vault name
      --vaultpass=<password>       vault password
    EOF
    say $HELP.trim;
}

multi sub USAGE('ls')
{
    constant $HELP = q:to/EOF/;
    Usage:
      holovault ls keymaps
      holovault ls locales
      holovault ls partitions
      holovault ls timezones
    EOF
    say $HELP.trim;
}

# vim: set filetype=perl6 foldmethod=marker foldlevel=0:
